plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

apply from: '../clientKeys.gradle'

Properties localProperties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    localProperties.load(propertiesFile.newDataInputStream())
}

android {
    namespace "ru.modulkassa.payment.demo"
    compileSdk versions.compileSdk

    def (merchantId, signatureKey) = getClientKeys()

    defaultConfig {
        applicationId "ru.modulkassa.payment.demo"
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "MERCHANT_ID", "\"${merchantId}\""
        buildConfigField "String", "SIGNATURE_KEY", "\"${signatureKey}\""
    }

    signingConfigs {
        release {
            storeFile = file("keystore/demo-keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: localProperties.getProperty("android.storePassword")
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: localProperties.getProperty("android.keyAlias")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: localProperties.getProperty("android.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(':payment-library')
    implementation libraries.kotlin
    implementation libraries.material
    implementation libraries.appcompat
    implementation libraries.constraintlayout

    testImplementation libraries.junit
}